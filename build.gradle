plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.ajoberstar.grgit' version '5.0.0'
}

group 'net.william278'
version "2.0.1-${versionMetadata()}"

repositories {
    mavenCentral()

    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://jitpack.io/' }
    maven { url 'https://repo.minebench.de/' }
    maven { url 'https://repo.codemc.io/repository/maven-snapshots/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.16.5-R0.1-SNAPSHOT'
    compileOnly 'net.william278:HuskHomes2:06d73c23d1'
    compileOnly 'org.jetbrains:annotations:24.0.0'

    implementation 'de.themoep:inventorygui:1.6-SNAPSHOT'
    implementation 'org.apache.commons:commons-text:1.10.0'
    implementation 'de.themoep:minedown-adventure:1.7.2-SNAPSHOT'
    implementation 'net.kyori:adventure-text-serializer-legacy:4.12.0'
    implementation 'net.kyori:adventure-platform-bukkit:4.2.0'
    implementation 'net.william278:Annotaml:2.0.1'
    implementation 'dev.dejvokep:boosted-yaml:1.3'
    implementation 'net.wesjd:anvilgui:1.6.3-SNAPSHOT'
    implementation 'net.william278:DesertWell:1.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

shadowJar {
    relocate 'de.themoep.inventorygui', 'net.william278.huskhomes.gui.libraries.inventorygui'
    relocate 'de.themoep.minedown', 'net.william278.huskhomes.gui.libraries.minedown'
    relocate 'net.kyori', 'net.william278.huskhomes.gui.libraries'
    relocate 'net.wesjd', 'net.wesjd.anvilgui'
    relocate 'net.william278.annotaml', 'net.william278.huskhomes.gui.libraries.annotaml'
    relocate 'dev.dejvokep', 'net.william278.huskhomes.gui.libraries.boostedyaml'
}

jar {
    dependsOn('shadowJar')
}

test {
    useJUnitPlatform()
}

@SuppressWarnings('GrMethodMayBeStatic')
def versionMetadata() {
    if (grgit == null) {
        return System.getenv("GITHUB_RUN_NUMBER") ? 'build.' + System.getenv("GITHUB_RUN_NUMBER") : 'unknown'
    }
    return grgit.head().abbreviatedId + (grgit.status().clean ? '' : '-indev')
}